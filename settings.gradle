rootProject.name = 'toby-spring-boot'


["project"].each {  // 프로젝트가 생성될 상위 디렉토리를 생성한다.

    def compDir = new File(rootDir, it)
    if (!compDir.exists()) {
        compDir.mkdirs()
    }

    compDir.eachDir {subDir ->  // 프로젝트를 생성

        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if (!gradleFile.exists()) {
            gradleFile.text =
                    """

                    dependencies {
                    
                    }

                    """.stripIndent(20)
        }

        [
                "src/main/java/com/sandro",
                "src/main/resources",
                "src/test/java/com/sandro",
                "src/test/resources"
        ].each {srcDir ->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if (!srcFolder.exists()) {
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}";
        include projectName
        project(projectName).projectDir = subDir
    }
}